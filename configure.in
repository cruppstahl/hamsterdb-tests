dnl
dnl configuration script for hamsterdb
dnl
dnl Copyright (C) 2005-2007 Christoph Rupp (chris@crupp.de).
dnl All rights reserved. See file LICENSE for licence and copyright
dnl information
dnl

dnl Initialize autoconf/automake
AC_INIT(hamsterdb-tests, 0.1)
AC_CANONICAL_HOST
AC_CONFIG_SRCDIR(src/hamsterdb.cpp)
AC_CONFIG_MACRO_DIR(m4)
AM_INIT_AUTOMAKE

AM_CONFIG_HEADER(config.h)
AC_COPYRIGHT([
Copyright (C) 2005-2011 Christoph Rupp (chris@crupp.de)
])

AC_PROG_CC
AC_PROG_CC_STDC
AC_USE_SYSTEM_EXTENSIONS
AC_PROG_CXX
LT_INIT # replaces AC_PROG_RANLIB
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_SED
AC_HEADER_STDC
AC_C_CONST
AC_TYPE_SIZE_T
AC_CHECK_FUNCS(mmap munmap getpagesize fdatasync fsync)
AC_CHECK_HEADERS(fcntl.h unistd.h malloc.h)
AC_TYPE_OFF_T
AC_FUNC_MMAP

dnl 
dnl check for cygwin/Win32
dnl
case $host in 
*-*-cygwin*)
    CFLAGS="${CFLAGS} -DWIN32 -DCYGWIN"
    ;;
*-*-mingw32*)
    CFLAGS="${CFLAGS} -DWIN32"
    ;;
esac

dnl
dnl use -Wall on platforms with gcc
dnl
if test "x$GCC" = "xyes"; then
    if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
        CFLAGS="$CFLAGS -Wall"
    fi
fi

dnl
dnl check for little endian/big endian
dnl
AC_C_BIGENDIAN(
        ac_big_endian=yes,
        ac_big_endian=no,
        ac_big_endian=no)
if test $ac_big_endian = yes; then
    CFLAGS="${CFLAGS} -DHAM_BIG_ENDIAN"
else
    CFLAGS="${CFLAGS} -DHAM_LITTLE_ENDIAN"
fi

dnl
dnl support debugging
dnl 
AC_ARG_ENABLE(debug,
     [  --enable-debug          Turn on debugging],
     [case "${enableval}" in
       yes) debug=true ;;
       no)  debug=false ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
     esac],[debug=false])
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

dnl
dnl support profiling
dnl 
AC_ARG_ENABLE(profile,
     [  --enable-profile        Turn on profiling],
     [case "${enableval}" in
       yes) profile=true ;;
       no)  profile=false ;;
       *) AC_MSG_ERROR(bad value ${enableval} for --enable-profile) ;;
     esac],[profile=false])
AM_CONDITIONAL(PROFILE, test x$profile = xtrue)

AC_OUTPUT(Makefile src/Makefile)

